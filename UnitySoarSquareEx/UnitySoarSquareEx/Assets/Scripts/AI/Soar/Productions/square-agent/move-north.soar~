############################ Move-north operator ############################
# Propose*move-north:
# If I exist, then propose the move-north operator.
sp {propose*move-north
    (state <s>  ^name square-agent
                ^io.input-link.square.position <position>)
    (<position> ^x <x> ^y <y>)
-->
    (<s> ^operator <o> +)
    (<o> ^name move-north)
    (write |Proposed North x:|<x>| y:|<y>||)
}

# Apply*move-north:
# If the move-north operator is selected, then generate an output command to
# move north.
sp {square-agent*apply*move-north
    (state <s> ^operator.name move-north
               ^io.output-link <out>)
-->
    (<out> ^move.direction north)
    (write |Apply North|)
}

# Apply*move-north*remove-move:
# If the move-north operator is selected,
# and there is a completed move command on the output link,
# then remove that command.
sp {apply*move-noth*remove-move
    (state <s> ^operator.name move-north
               ^io.output-link <out>)
    (<out> ^move <move>)
    (<move> ^status complete)
-->
    (<out> ^move <move> -)
    (write |Remove North|)
}